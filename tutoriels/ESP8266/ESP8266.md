# Collectez des données sur le web avec vos ESP8266 : du code !

*Après avoir vu toute la partie théorique de ce qui fait les échanges entre un navigateur et un site web, et découvert comment espionner et analyser ces communications, il est temps de passer à quelque chose d'un peu plus créatif. Armés de toutes les informations nécessaires, nous pouvons enfin nous pencher sur le code et commencer à obtenir des résultats.*

Nous travaillerons ici par étapes, abattant chaque difficulté l’une après l’autre, pour arriver jusqu'au but. Pour rappel, notre objectif est d'extraire des informations numériques de pages d'administration d'une boutique en ligne de type PrestaShop. Au menu, nous devons gérer le HTTPS, passer l'authentification en place, prouver notre identité à l'interface d'administration et arriver à lire une page autrement inaccessible. Sans plus attendre, attelons-nous à la tâche !

## 1. Établir une connexion HTTPS

Nous passerons sous silence ici toute la partie standard de connexion au wi-fi, qui se fait tout à fait classiquement, pour nous intéresser uniquement à l'objet de l'article. Notez au passage un conseil qui peut vous être utile : j'ai pris pour habitude de systématiquement, pour chaque projet impliquant un ESP8266, enregistrer dans l'EEPROM émulée le SSID du point d'accès, le mot de passe associé et le nom d'hôte de l'ESP8266 (voir Hackable 26 et l'article sur MQTT). Le croquis de départ vierge pour débuter un projet avec cette carte intègre donc, de base, le code pour lire ces données qui ne sont pas écrasées lors de la reprogrammation de l'ESP8266, se connecter au wi-fi et configurer la mise à jour OTA (voir Hackable 21).

![DSP-0401B_1](espweb_aff4digits_large.jpg)

Ce type d'afficheur à LED est très intéressant bien que relativement coûteux (environ 25€ pour ce DSP-0401B de chez Embedded Adventures). Il se pilote à l'aide de quelques connexions, est compatible avec des signaux en 3,3v et permet surtout d'afficher aussi bien des chiffres que du texte sur des afficheurs 16 segments de 35 mm de haut, qui se voient de loin.

Revenons à notre sujet, pour établir une connexion HTTPS, nous ne nous occupons pas de HTTP, mais nous nous connectons simplement au serveur sur le port 443, en utilisant SSL/TLS. HTTPS est en réalité HTTP sur SSL/TLS. Cette connexion représente la couche chiffrée et ce sera à nous de « parler » HTTP avec le serveur, via ce canal de communication protégé. SSL/TLS n'assure pas que le chiffrement des communications, ce protocole permet également de sécuriser cryptographiquement l'identité des intervenants et en particulier, du serveur. En tant que client, nous pouvons vérifier ce point en inspectant l'empreinte du certificat du serveur (grossièrement, une version condensée de sa carte d'identité).

Cette empreinte du certificat, ou fingerprint, peut être obtenue depuis le navigateur, en cliquant au début de l'URL et en cliquant sur « Certificat » (dans Chromium). Dans les détails du certificat utilisé par le site qui apparaissent, repérez l'empreinte SHA-1 et copiez-là dans votre code. D'autres fonctions de hachage cryptographiques peuvent théoriquement être utilisées (comme SHA-256 à la place de SHA-1, si l'empreinte est disponible dans le certificat), mais il est généralement d'usage de prendre la plus concise afin d'économiser la mémoire.

Une fois cette information obtenue, nous pouvons débuter notre croquis :

```
#include <ESP8266WiFi.h>
#include <WiFiClientSecure.h>

// site
const char* host = "boutique.com";
// port IP (défaut HTTPS)
const int httpsPort = 443;
 
// Empreinte SHA-1 du certificat
const char* fingerprint = "0B 0D 94 6B 4B E8 63 56 C7 CC 4E 47 A2 96 5B B6 27 F5 27 B3";
```

Nous avons ici quelques déclarations de variables pour l'hôte (le site web), le port utilisé et l'empreinte. Notez l'utilisation de la bibliothèque WiFiClientSecure en lieu et place de WiFiClient ou ESP8266HTTPClient afin de pouvoir gérer les connexions SSL/TLS. Nous pouvons ensuite nous pencher sur la base de ce qui sera notre fonction de collecte d'information :

```
void webcollect() {
   WiFiClientSecure client;
 
   Serial.println(F("Connexion au serveur Web"));
   if (!client.connect(host, httpsPort)) {
      Serial.println(F("Erreur connexion serveur web!"));
      return;
   }
 
   // vérification certificat SSL/TLS
   if (client.verify(fingerprint, host)) {
      Serial.println(F("Certificat ok"));
   } else {
      Serial.println(F("Mauvais certificat !"));
      client.stop();
      return;
   }
 
   client.stop();
}
```

Rien de bien extraordinaire ici, nous avons notre fonction `webcollect()` qui établit la connexion avec `client.connect()` en précisant l'hôte et le port. Comme __client__ est un objet de type `WiFiClientSecure`, la connexion sera automatiquement en SSL/TLS. Une fois la connexion établie, nous pouvons alors utiliser la méthode `verify()` en spécifiant, en argument, l'empreinte et le nom d'hôte du serveur. Si l'empreinte correspond, nous pourrons nous occuper de la suite et si ce n'est pas le cas, nous terminons la connexion et quittons la fonction.

## 2. Utiliser l'authentification Basic

Nous sommes maintenant en mesure de communiquer avec le serveur de façon sécurisée. Il ne nous reste donc plus qu'à discuter avec lui en HTTP via ce canal de communication fraîchement établi. Comme nous avons une authentification, la première chose que nous devons pouvoir achever est le fait d'obtenir une page web de la part du serveur.

Le type d'authentification auquel nous avons affaire est dite « HTTP Basic ». Ceci consiste à envoyer un nom d'utilisateur et un mot de passe au serveur dans l'en-tête de la requête. Si ce couple est valide et autorisé, le serveur affiche la page, sinon il retourne un code 401. Votre navigateur fait, encore une fois, cela de façon transparente. En tentant d'accéder à une ressource de ce type, il reçoit en réponse un code 401 et affiche un formulaire vous demandant de spécifier les informations requises. Tant que vous entrerez de mauvaises informations, ce formulaire se réaffichera jusqu'à ce que vous appuyiez sur Echap et, là seulement, vous verrez le message d'erreur incluant le code 401.

![DSP-0401B_2](espweb_espback_large.jpg)

*Un autre avantage des modules d'affichage à LED vendus « tout-fait » est la possibilité de directement intégrer l'ESP8266 à l'arrière. Il faut un peu bricoler avec les connexions, mais on arrive relativement facilement à dissimuler un Wemos DI mini à l'arrière. Malheureusement, l'alimentation en 5v des segments n'est possible que pour certaines couleurs (rouge et jaune). Pour les autres (vert, blanc et bleu) l'ESP8266 ne pourra fournir la tension demandée.*

Dans notre cas, nous n'allons pas nous amuser à demander ces informations à qui que ce soit et nous les embarquons simplement dans l'en-tête de notre requête. Il est également possible de faire cela avec votre navigateur en utilisant une URL comme https://utilisateur:mot2passe@site.domaine.com. Dans ce cas, le formulaire ne s'affichera pas (si les informations sont correctes) et la page demandée sera affichée. Nous ne pouvons pas utiliser cette syntaxe dans notre croquis, car c'est une facilité offerte par le navigateur qui prend les informations, les encode et les utilise pour créer sa requête. Nous, nous allons devoir faire ce travail nous-mêmes.

Une requête HTTP n'est pas quelque chose de bien complexe. Si nous voulons, par exemple, obtenir la page `toto.html` sur le site boutique.com, il nous suffit de formuler la requête suivante :

```
GET /toto.html HTTP/1.1\r\n
Host: boutique.com\r\n
\r\n
```

Nous avons en première ligne la méthode utilisée (`GET`), suivie de la ressource demandée (`/toto.html`) et de la mention du protocole utilisé (`HTTP/1.1`). Chaque ligne se termine systématiquement par les deux caractères spéciaux CR (Carriage Return, `0x0D`) et LF (Line Feed, `0x0A`). La seconde ligne précise un en-tête spécifiant le nom d'hôte concerné. Ceci est obligatoire avec HTTP version 1.1 et permet à un seul serveur d'héberger plusieurs sites (notion d'hôtes virtuels). La requête ne comportant ici pas de corps, elle se termine simplement par une ligne vide.

Ceci est un strict minimum, mais il est d'usage de préciser d'autres en-têtes comme, par exemple, celui permettant de préciser le client web (ou agent) utilisé :

```
User-Agent: curl/7.55.0\r\n
```

Ceci peut tantôt être important, si le serveur réagit de façon différente en fonction du navigateur qu'il détecte. J'ai ici précisé l'utilisation de Curl, un outil en ligne de commande pour GNU/Linux, mais il est relativement simple de faire semblant d'être un vrai navigateur, en réutilisant simplement son *User Agent*.

![DSP-0401B_3](espweb_bellephoto_large.jpg)

*On voit ici clairement les 16 segments qui composent une position de l'afficheur. Bien entendu, en fonction de l'information que vous allez collecter sur le web et donc afficher, un module 7 segments pourra être suffisant (et plus économique). Il s'avère que dans mon cas, je n'en avais pas sous la main.*

Procéder à ce type de requêtes dans un croquis, une fois la connexion établie, est relativement simple :

```
// Nouvelle requête
client.print(String("GET /toto.html HTTP/1.1\r\n")
   + "Host: boutique.com\r\n"
   + "User-Agent: curl/7.55.0\r\n"
   + "\r\n"
   );
```

Le simple fait de d'utiliser la méthode `print()` sur l'objet représentant notre connexion fait l'affaire et il nous suffit de jouer avec la concaténation des chaînes pour obtenir quelque chose de visuellement intelligible. Pour traiter la réponse, une simple boucle fera l'affaire :

```
String line;
 
// Récupération/affichage en-tête
while(client.connected()) {
   line = client.readStringUntil('\n');
   if (line == "\r") {
      Serial.println(F("en-tête terminé"));
      break;
   }
   line.trim();
   Serial.print("> ");
   Serial.println(line);
}
 
Serial.println(F("Fermeture connexion"));
client.stop();
```

Ici, nous nous limitons à l'affichage de l'en-tête de la réponse et coupons la connexion immédiatement une fois la ligne vide reçue. Ce code devrait vous renvoyer, dans le moniteur série, quelque chose comme :

```
> HTTP/1.1 200 OK
> Date: Wed, 09 Jan 2019 15:26:06 GMT
> Content-Type: text/html; charset=UTF-8
> Transfer-Encoding: chunked
> Server: Apache
> X-Powered-By: PHP/5.2.17
> Expires: Wed, 11 Jan 1984 05:00:00 GMT
```

« Et mon authentification dans tout ça ? » me direz-vous. Il ne s'agit que d'une information à glisser dans l'en-tête de la requête, mais ceci doit se faire d'une certaine manière. Il s'agit de l'en-tête `Authorization:`, s'utilisant exactement comme `User-Agent`: ou `Host:` et en précisant la méthode d'authentification (ici `Basic`), suivi des informations « utilisateur:mot2passe » encodées en base64.

Vous avez plusieurs solutions pour générer cette information dans le format attendu :
- tout simplement, inspecter les échanges entre le navigateur et le site comme nous l'avons fait précédemment ; cette information est présente dans l'en-tête de la requête et correspond à ce que vous avez saisi dans le formulaire, présenté par le navigateur ;
- utiliser un outil en ligne de commandes sous GNU/Linux, comme `base64` pour obtenir la version encodée ; par exemple, `echo -n “utilisateur:passe” | base64` vous affichera `dXRpbGlzYXRldXI6cGFzc2U= ;`
- utiliser un encodeur/décodeur base64 en ligne qui, en entrant une chaîne de caractères, vous affichera la version encodée ; étant donnée la nature secrète de l'information, je ne recommande pas cette méthode (entrer un mot de passe sur un site, qui n'est pas celui à qui il est destiné, n'est jamais une bonne idée).

Une fois le duo nom d'utilisateur et mot de passe, séparé par un « : », encodé en base64, il vous suffit d'ajouter une ligne dans votre requête (et toutes les suivantes) :

```
// Nouvelle requête
client.print(String("GET /toto.html HTTP/1.1\r\n")
   + "Host: boutique.com\r\n"
   + "User-Agent: curl/7.55.0\r\n"
   + "Authorization: Basic dXRpbGlzYXRldXI6cGFzc2U=\r\n"
   + "\r\n"
   );
```

et la requête fonctionnera comme s'il n'y avait pas d'authentification nécessaire.

## 3. Poster un formulaire et utiliser les cookies

La méthode HTTP `GET` permet de demander une ressource (page HTML) à un serveur, et c'est ultimement ce que nous ferons pour obtenir l'information que nous souhaitons avoir. Mais pour pouvoir demander cette ressource, il nous faut tout d'abord montrer patte blanche et commencer par poster le formulaire permettant de nous faire accepter par le site. Ceci passe par l'utilisation d'une requête `POST`.

En observant attentivement le contenu de la page proposant le formulaire de connexion, ainsi que les échanges entre le navigateur et le site, nous pouvons assez facilement trouver quelles informations sont envoyées. Un formulaire HTML affiche des zones de saisie qui peuvent avoir plusieurs types (`text`, `password`, `hidden`, `submit`, etc.) mais au final, ceci n'aura pas d'impact sur les données effectivement envoyées. Celles-ci prennent la forme de contenu dans la requête, exactement comme une page HTML est le contenu de la réponse du site. Nous pouvons donc, sur la base de ces informations, construire notre requête comme précédemment :

```
// Nouvelle requête
client.print(F("POST /admin/index.php?controller=AdminLogin HTTP/1.1\r\n"
   "Host: boutique.com\r\n"
   "User-Agent: curl/7.55.0\r\n"
   "Authorization: Basic dXRpbGlzYXRldXI6cGFzc2U=\r\n"
   "Content-Type: application/x-www-form-urlencoded\r\n"
   "Content-Length: 59\r\n"
   "\r\n"
   "email=utilisateur%40domaine.fr&passwd=mot2pass&submitLogin="));
```

Vous reconnaîtrez sans peine une partie des en-têtes qui sont parfaitement identiques à la requête précédente. Cependant, plusieurs points sont importants :

- nous utilisons une méthode `POST` et non `GET` ;
- nous n'oublions pas de spécifier l'authentification Basic ;
- la ressource concernée embarque ici une *query string* ;
- nous utilisons l'en-tête `Content-Type:` afin de préciser qu'il s'agit du contenu d'un formulaire dans un format particulier ;
- nous devons préciser la taille du contenu que nous envoyons avec l'en-tête `Content-Length:` ;
- et enfin, les données sont passées avec une syntaxe et avec un encodage particulier.

Ce dernier point est très important. Non seulement, nous devons présenter les données sous la forme d'une suite de champs accompagnés de leur valeur (`passwd=mot2pass` par exemple) et séparés par des `&`, mais les informations doivent être partiellement traduites avec un mécanisme d'encodage appelé *percent-encoding* ou *URL encoding*. Suivant cet encodage, certains caractères, dont le `=` et le `&`, ont une signification particulière. Le `&` sert par exemple de séparateur, il ne peut donc pas être présent dans une donnée, sinon il serait interprété comme une délimitation.

En fonction de vos informations à envoyer donc, une traduction (un encodage en réalité) sera nécessaire. On peut voir ici que le `@` de l'adresse mail utilisée comme identifiant d'utilisateur n'est pas présent, mais remplacé par `%40`. Comme avec l'encodage base64, il existe des outils et des services en ligne permettant d'encoder vos informations pour ensuite les copier/coller dans votre croquis. Mais le plus rapide est de tout simplement, une fois encore, récupérer ces informations dans la capture qu'aura fait votre navigateur pour la requête `POST`.

Dans le cas présent, une authentification réussie aura pour effet une réponse avec un code 302 de la part du serveur, accompagnée de la définition d'un cookie qu'il faudra ensuite présenter lors de la ou des requêtes suivantes. Si l'authentification échoue, le serveur répondra en envoyant à nouveau la page HTML de connexion. Un code 302 est une redirection et le serveur nous invite à nous rendre sur une nouvelle page. Il n'est en principe pas nécessaire de suivre cette redirection, car dans bien des cas, la réutilisation du cookie sera suffisante pour accéder à n'importe quelle page.

![DSP-0401B_4](espweb_bleurouge_large.jpg)

*Le rendu de ce type de module est du plus bel effet, sachant que les couleurs peuvent être utilisées pour différencier les informations. Leur coût pourra être justifié en partie par le fait de s'en servir pour plus d'une tâche avec, comme ici, l'affichage de l'heure et de la température extérieure, alterné avec celui des données collectées.*

La requête de la partie précédente (le `GET`) n'était qu'un simple test, nous n'avons pas réellement besoin d'accéder à la page de formulaire avant le `POST`. Nous partons ici du principe que le formulaire et donc, les informations à envoyer, ne changeront pas, sauf si l'applicatif web est changé. Si tel était le cas, il faudra certes revoir le script, mais cette solution est plus intéressante qu'un fonctionnement totalement dynamique, qui reviendrait à commencer le développement d'un navigateur.

Notre requête `POST` est donc fiable, il ne nous reste plus qu'à analyser la réponse du serveur et pour cela, nous commençons par déclarer quelques variables :

```
// URL de redirection
String newurl;
// Cookie
String prestacookie;
// ligne de réception
String line;
// Données à récupérer
int nbr1 = 0;
int nbr2 = 0;
```

Comme précédemment, nous allons utiliser une boucle `while()` afin de séquentiellement traiter les lignes de la réponse donnée par le site. Nous ne nous intéressons qu'aux données se trouvant dans l'en-tête de la réponse, puisque nous sommes censés obtenir une redirection et non des données, en cas de connexion réussie. La première étape consistera à évaluer le code de réponse HTTP :

```
while(client.connected()) {
   line = client.readStringUntil('\n');
   // test code réponse
   if(line.startsWith(F("HTTP/1.1"))) {
      if(!line.startsWith(F("HTTP/1.1 302"))) {
         Serial.println(F("Erreur authentification !"));
         Serial.println(F("Fermeture connexion"));
         client.stop();
         return;
      }
   }
```

La méthode `startsWith()` nous permet de tester la présence d'une chaîne de caractères au début d'une autre. Ainsi, nous pouvons vérifier si, dans un premier temps, il s'agit de la ligne correspondant au code de statut et si tel est le cas, si nous avons bien une redirection temporaire comme espérée (302). Nous sommes obligés de procéder de la sorte, car le standard précise que l'ordre des en-têtes n'est pas significatif. Nous ne savons donc pas quelle ligne contient la donnée attendue. Quoi qu'il en soit, si nous n'avons pas un 302, nous quittons la fonction de collecte après avoir fermé la connexion.

![DSP-0401B_5](espweb_LCD_large.jpg)

*L'afficheur LCD est également une option valable pour ce type d'usage, à condition de se satisfaire de deux limitations importantes : une dimension réduite et la difficulté de trouver un tel module capable de fonctionner avec des signaux 3,3v (ce qui n'est pas le cas de ce modèle).*

Nous devons également traiter l'URL de redirection. Initialement, mon idée n'était pas de la suivre, mais de simplement extraire le token présent dans l'URL sous la forme d'une *query string* pour le réutiliser à volonté. Il s'avère malheureusement que celui-ci fonctionne comme une sorte de somme de contrôle (ou un condensé) calculée sur l'URL complète, le token est ensuite chiffré avec une information d'authentification de l'utilisateur. Ce qu'il faut comprendre ici, c'est tout simplement que le token change pour chaque page et qu'il faudrait donc, en principe, simuler une navigation pour arriver à la page de notre choix. Je rappelle que ceci est une spécificité de l'applicatif web PrestaShop et non un cas courant.

Fort heureusement, dans ce cas précis, nous avons une solution : il est possible, dans l'interface d'administration, de spécifier sa page de départ en tant qu'utilisateur/administrateur (*admin homepage*). Ceci signifie que dès la connexion réussie, PrestaShop redirige l'utilisateur vers une page précise avec, dans l'URL, un token valide. Il nous suffit donc de configurer la page qui nous intéresse pour notre utilisateur et suivre effectivement la redirection. Ce qui simplifie grandement le code sur ce point :

```
// récupération URL de redirection
if(line.startsWith(F("Location: "))) {
   newurl = line.substring(10);
   newurl.trim();
}
```

Enfin, dans l'en-tête de la réponse se trouve également le cookie qui permet au site de savoir, dans les requêtes qui suivent, que nous sommes bien connectés. Nous devons donc le récupérer et le stocker :

```
// récupération cookie
if(line.startsWith(F("Set-Cookie: PrestaShop-"))) {
   int pos = line.indexOf(';',12);
   if(pos>0) {
      prestacookie = line.substring(12,pos);
   }
}
```

Nous y avons échappé pour le token, mais nous devons, pour le cookie, jouer avec les chaînes de caractères. Certes, la classe `String` n'est pas une solution très économe en mémoire (SRAM) et il est possible de faire de même avec des tableaux de caractères C/C++, mais il faut bien avouer qu'avec un ESP8266, les méthodes de cette classe ont souvent plus d'avantages que d’inconvénients. Nous jouons donc du `startsWith()` pour sélectionner la bonne ligne, puis du `indexOf()` pour trouver la fin de la chaîne qui nous intéresse et enfin du `substring()` pour utiliser sa position afin d'obtenir, dans `prestacookie`, une valeur utilisable.

Pour terminer, comme seuls les en-têtes de la réponse nous intéressent, nous pouvons mettre fin à la boucle `while()` avec un `break` pour passer à la suite :

```
// fin de l'en-tête HTTP
   if (line == "\r") {
      Serial.println(F("en-tête ok"));
      break;
   }
}
```

## 4. Faire la requête et extraire les données

Nous disposons à présent des éléments nécessaires pour une ultime requête, consistant à demander la page contenant les informations souhaitées. Il existe ici une énième spécificité concernant PrestaShop. En effet, même en configurant une page d'accueil pour l'utilisateur dans l'interface d'administration, l'URL fournie dans la réponse 302 du serveur est incomplète. Lorsqu'on observe les échanges entre un navigateur et le site, il s'avère que l'URL en question, lorsqu'elle est accédée, découle sur une nouvelle redirection avec l'URL finale.

Chose intéressante, la nouvelle URL est exactement la même que la précédente, mais voit une chaîne ajoutée à sa fin. On remarque, pourtant, que le token ne change pas et nous pouvons donc en conclure que tout ce qui est ajouté après la mention du token dans l'URL n'impacte pas sa valeur. C'est une véritable aubaine pour nous, puisque tout ce que nous avons à faire est d'ajouter cette chaîne à l'URL de redirection originale, pour tomber directement sur la bonne page.

Notre nouvelle requête ressemblera donc à ceci :

```
client.print(String("GET /admin/") + newurl + "&ChaineSupplementaire" + " HTTP/1.1\r\n"
   + "Host: boutique.ed-diamond.com\r\n"
   + "User-Agent: curl/7.55.0\r\n"
   + "Authorization: Basic dXRpbGlzYXRldXI6cGFzc2U=\r\n"
   + "Cookie: " + prestacookie + ";\r\n"
   + "\r\n"
);
```

Je préciserais, à toutes fins utiles, que bien entendu, la requête ici présente a été éditée avant d'être intégrée dans l'article (comme toutes les informations d'authentification). Quoi qu'il en soit, sa structure est identique à celle que vous pourriez vous-même utiliser dans vos développements. On y retrouve les éléments déjà connus, comme `Authorization:`, mais aussi et surtout `Cookie:` qui permet au client de présenter le cookie que le site lui aura précédemment confié.

![DSP-0401B_6](BigDigits_inSitu_large.jpg)

*Le fait de construire son propre afficheur numérique à partir de rubans de LED, comme nous l'avions fait dans le numéro 11, est une option qui intéressera sans doute les plus manuels. Ici, pas de limitation en termes de dimension des chiffres, mais il faudra être un bon bricoleur pour obtenir un résultat propre.*

Nous utilisons ensuite les mêmes techniques pour analyser tout d'abord l'en-tête de la réponse :

```
// Récupération en-tête
while(client.connected()) {
   line = client.readStringUntil('\n');
   if (line == "\r") {
      Serial.println(F("en-tête ok"));
      break;
   }
}
```

Puis le corps :

```
// Analyse de la page HTML
while(client.connected()) {
   line = client.readStringUntil('\n');
   if(line.indexOf("Magazine <b>Hackable Magazine") != -1) {
      line = client.readStringUntil('\n');
      line.trim();
      if(!nbr1) {
         nbr1 = line.substring(line.indexOf("<b>")+3).toInt();
      } else {
         nbr2 = line.substring(line.indexOf("<b>")+3).toInt();
      }
   }
   if(nbr1 && nbr2) {
      Serial.println(F("Informations obtenues"));
      break;
   }
}
 
Serial.println(F("Fermeture connexion"));
client.stop();
```

L'analyse de la page reçue est totalement spécifique à son contenu et sa structure. Ici, nous traitons ligne par ligne les données HTML obtenues, sachant que les deux valeurs numériques qui nous intéressent sont présentes sur une ligne, précédée d'une autre faisant mention du nom du magazine. L'idée est donc de repérer l'une de ces lignes et d'en obtenir immédiatement une nouvelle, d'où extraire la donnée. Nous utilisons la méthode `indexOf()` pour trouver la position d'une chaîne précise et utilisons la valeur obtenue pour extraire la sous-chaîne (`substring()`), sur laquelle nous utilisons la méthode `toInt()` pour obtenir une valeur entière. Nous partons du principe ici que les deux valeurs qui nous intéressent apparaissent successivement, la première allant dans la variable `nbr1` et la seconde dans `nbr2`. Là encore, si quelque chose change sur la page, il faudra changer le croquis.

Au terme de l'exécution de cette boucle et après la fermeture de la connexion avec `client.stop()`, nous sommes censés avoir dans `nbr1` et `nbr2` des entiers non nuls, correspondants aux données que nous voulions récupérer. Il est alors possible de les utiliser pour un affichage sur le moniteur série ou sur un module connecté à l'ESP8266. Dans mon cas, j'ai fait usage d'un afficheur 16 segments à LED basé sur un TLC5926, vendu par *Embedded Adventures* et pour lequel, j'avais écris une bibliothèque Arduino faisant l'objet d'un article dans le numéro 5 (https://github.com/0xDRRB/DSP0401B).

Pour terminer, la fonction `loop()` consiste à simplement déclencher un appel récurrent à la fonction `webcollect()` en fonction du temps écoulé, mesuré avec `millis()` :

```
void loop() {
   unsigned long currentMillis = millis();
   if (currentMillis - previousMillis >= (MMINUTE*30)) {
      previousMillis = currentMillis;
      webcollect();
   }
   // gestion OTA
   ArduinoOTA.handle();
}
```

Notez l'utilisation d'une macro `MMINUTE` simplifiant la spécification des délais et faisant partie d'une série que j'inclus presque systématiquement à mes croquis Arduino :

```
#define MSECOND 1000
#define MMINUTE 60*MSECOND
#define MHOUR 60*MMINUTE
#define MDAY 24*MHOUR
```

## 5. Pour finir

Ce long article présente une situation bien particulière et une démarche jonchée d'embûches, mais qui cependant présente l'avantage d'être très certainement plus complexe que la situation à laquelle vous pourriez être confronté. Dans bien des cas, il n'est pas si difficile d'obtenir l'information voulue, puisque celle-ci se trouvera sur une page visible publiquement. Même en prenant en compte une éventuelle connexion via un formulaire, il devrait vous être relativement facile de faire de même. Bien entendu, chaque site possédera ses spécificités avec lesquelles vous devrez composer, mais HTTPS, plus l'authentification Basic, plus le formulaire, plus le token, plus les redirections inattendues... forment un mélange relativement rare.

Bien entendu, une approche plus classique est parfois possible avec les mécanismes dédiés au développement que sont les API Web et autres *webservices*. Malheureusement, il faut également bien comprendre que ce n'est pas parce qu'une API est disponible, qu'elle permet forcément de faire ce que vous souhaitez. Si nous prenons l'exemple du moteur de blog WordPress par exemple, une bonne partie des fonctionnalités de base sont utilisables ainsi, mais dans une très grande majorité des cas, ceci ne sera pas valable pour les extensions installées. Les fonctionnalités exposées par une éventuelle API sont totalement dépendantes du bon vouloir des développeurs de cette dernière.

Enfin, je préciserai que, même si la solution consistant à créer un client web comme nous l'avons fait ici reste la voie la plus hasardeuse, c'est aussi celle qui est la moins intrusive. En effet, il n'y a aucun changement à faire sur le site visé, aucune fonctionnalité à activer et aucun greffon à ajouter. Tout se passe comme si l'ESP8266 était une connexion parfaitement normale de la part d'un navigateur. Ceci conviendra donc parfaitement pour un site sensible, où il vaut mieux ne rien toucher...
